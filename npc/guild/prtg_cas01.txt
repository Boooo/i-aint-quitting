//====================================================================
//          _________                                                  
//          \_   ___ \_______  ____   ____  __ __  ______             
//          /    \  \/\_  __ \/    \ /    \|  |  \/  ___/              
//          \     \____|  | \(  ( ) )   |  \  |  /\___ \               
//           \______  /|__|   \____/|___|  /____//____  >              
//                  \/                   \/           \/               
//                                                               
//====================== Cronus Script ===============================
//= Guerra do Emperium (Prontera)
//===== Por: =========================================================
//= L0ne_W0lf
//===== Versão Atual: ================================================
//= 1.4
//===== Compatível com: ==============================================
//= Cronus SVN
//===== Descrição: ===================================================
//= NPCs relativos ao 1º Castelo de Prontera
//===== Comentários Adicionais: ======================================
//= 1.4 Script traduzido do eA conforme os diálogos do bRO. [Axl]
//====================================================================

// Bandeira do Castelo (Cidade)
//============================================================
prontera,155,190,4	script	Kriemhild#p1-1::Kriemhild	722,{
	end;

OnRecvCastlePt01:
	FlagEmblem GetCastleData("prtg_cas01",1);
	end;
}

// Bandeira do Castelo (Fora do Castelo)
//============================================================
prt_gld,131,60,6	script	Kriemhild#p1-2::Kriemhild2	722,{
	set .@GID, GetCastleData("prtg_cas01",1);
	if (.@GID == 0) {
		mes " [Decreto do Reino de Rune Midgard]";
		mes " ";
		mes "1. Conforme o Pacto do Reino de Rune Midgard";
		mes "e o Feudo das Valquírias,";
		mes "declaro que este Castelo não possui um Senhor.";
		mes " ";
		mes "2. Ao Clã que for bravo o bastante";
		mes "e, superarando os terríveis desafios,";
		mes "quebrar o Emperium,";
		mes "será dada a posse deste Castelo";
		mes "apoiado pelo nosso Rei.";
	}
	else {
		if (getcharid(2) == .@GID) {
			mes "[ Voz Ecoante ]";
			mes "Bravo guerreiro......";
			mes "Deseja retornar ao seu lugar de glória?";
			next;
			if (select("Sim.:Não.") == 1) {
				if (getcharid(2) == GetCastleData("prtg_cas01",1)) {
					warp "prtg_cas01",96,173;
					end;
				}
			}
			close;
		}
		mes "[Decreto do Reino de Rune Midgard]";
		mes " ";
		mes "1. Conforme o Pacto do Reino de Rune Midgard";
		mes "e o Feudo das Valquírias,";
		mes "declaro que este castelo pertence ao Clã ^ff0000" + GetGuildName(.@GID) + "^000000 .";
		mes " ";
		mes "2. O Líder do Clã ^ff0000"+ GetGuildName(.@GID) + "^000000 se chama";
		mes "^FF0000" + GetGuildMaster(.@GID) + "^000000";
		mes "Se alguém se opõe a este decreto";
		mes "desembainhe sua espada para fazer valer sua vontade.";
	}
	close;

OnRecvCastlePt01:
	FlagEmblem GetCastleData("prtg_cas01",1);
	end;
}

prt_gld,138,68,6	duplicate(Kriemhild2)	Kriemhild#p1-3	722
prt_gld,138,60,6	duplicate(Kriemhild2)	Kriemhild#p1-4	722
prt_gld,135,60,6	duplicate(Kriemhild2)	Kriemhild#p1-5	722

// Bandeira do Castelo (Dentro do Castelo)
//============================================================
prtg_cas01,58,56,4	script	Kriemhild#p1-4::Kriemhild3	722,{
	end;

OnRecvCastlePt01:
	FlagEmblem GetCastleData("prtg_cas01",1);
	end;
}

prtg_cas01,64,56,4	duplicate(Kriemhild3)	Kriemhild#p1-7	722
prtg_cas01,76,32,4	duplicate(Kriemhild3)	Kriemhild#p1-8	722
prtg_cas01,84,32,4	duplicate(Kriemhild3)	Kriemhild#p1-9	722
prtg_cas01,94,39,4	duplicate(Kriemhild3)	Kriemhild#p1-10	722
prtg_cas01,94,24,4	duplicate(Kriemhild3)	Kriemhild#p1-11	722
prtg_cas01,73,14,4	duplicate(Kriemhild3)	Kriemhild#p1-12	722
prtg_cas01,73,6,4	duplicate(Kriemhild3)	Kriemhild#p1-13	722
prtg_cas01,55,46,4	duplicate(Kriemhild3)	Kriemhild#p1-14	722
prtg_cas01,45,46,4	duplicate(Kriemhild3)	Kriemhild#p1-15	722

// Gerenciador Automático
//============================================================
prtg_cas01,197,197,0	duplicate(Gld_Agit_Manager)	Agit#prtg_cas01	-1

// Administrador do Castelo
//============================================================
prtg_cas01,112,181,0	duplicate(Gld_Mngr_Template)	Ahvray#prtg_cas01	55

// Guardiões
//============================================================
prtg_cas01,112,181,0	duplicate(Gld_Guard_Template)	Guardian#prtg_cas01	-1

// Serviço Kafra
//============================================================
prtg_cas01,96,173,0	duplicate(Gld_Kafra_Template)	Kafra#prtg_cas01	117

// Saída da Sala do Mestre
//============================================================
prtg_cas01,15,209,0	script	#switch_Pt01	111,{
	mes " ";
	mes "Há uma pequena alavanca, você deseja acioná-la?";
	next;
	if(select("Acionar.:Não acionar.") == 2) close;
	warp "prtg_cas01",112,183;
	end;
}

// Proteção de Tesouro da Guild
//============================================================
prtg_cas01,11,208,0	duplicate(Gld_Trea_Protect)	ban_warp#prtg_cas01	-1,6,6

// Spawns de Tesouro da Guild
//============================================================
prtg_cas01,11,208,0	duplicate(Gld_Trea_Spawn)	Treasure#prtg_cas01	-1

// Mudanças de Cavernas da Guild
//============================================================
prtg_cas01,94,200,0	duplicate(Gld_Dun_Template)	#prtg_cas01	111
